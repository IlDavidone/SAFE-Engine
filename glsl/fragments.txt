#version 330 core

out vec4 FragColor;

struct Material {
    sampler2D diffuse;
    sampler2D specular;
    float shininess;
    bool useSpecular;
};

struct DirectionalLight {
    vec3 direction;
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};

struct PointLight {
    vec3 position;

    float constant;
    float linear;
    float quadratic;

    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};

struct SpotLight {
    vec3 position;
    vec3 direction;
    float cutOff;
    float outerCutOff;

    float constant;
    float linear;
    float quadratic;

    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};

in VS_OUT {
    vec4 vertexColor;
	vec2 texCoord;
	vec3 FragPos;  
	vec3 Normal;
} fs_in;

uniform vec3 objectColor;
uniform vec3 viewPos;
uniform DirectionalLight dirLight;

#define MAX_POINT_LIGHTS 8
uniform int nrPointLights;
uniform PointLight pointLights[MAX_POINT_LIGHTS];
uniform SpotLight spotLight;
uniform Material material;

vec3 CalcDirectionalLight(DirectionalLight light, vec3 normal, vec3 viewDir);
vec3 CalcPointLight(PointLight light, vec3 normal, vec3 fragPos, vec3 viewPos);
vec3 CalcSpotLight(SpotLight light, vec3 normal, vec3 fragPos, vec3 viewDir);

void main()
{
    vec3 norm = normalize(fs_in.Normal);
    vec3 viewDir = normalize(viewPos - fs_in.FragPos);

    vec3 result;

    result = CalcDirectionalLight(dirLight, norm, viewDir);

    for (int i = 0; i < nrPointLights; i++)
        result += CalcPointLight(pointLights[i], norm, fs_in.FragPos, viewPos);

    //result += CalcSpotLight(spotLight, norm, FragPos, viewDir);

    FragColor = vec4(result * objectColor, 1.0);
}

/* ================= LIGHT FUNCTIONS ================= */

vec3 CalcDirectionalLight(DirectionalLight light, vec3 normal, vec3 viewDir)
{
    vec3 lightDir = normalize(-light.direction);
    float diff = max(dot(normal, lightDir), 0.0);

    vec3 reflectDir = reflect(-lightDir, normal);
    float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);

    vec3 texColor = texture(material.diffuse, fs_in.texCoord).rgb;

    vec3 ambient = light.ambient * texColor;
    vec3 diffuse = light.diffuse * diff * texColor;

    vec3 specular = vec3(0.0);
    if (material.useSpecular) {
        specular = light.specular * spec *
                   texture(material.specular, fs_in.texCoord).rgb;
    }

    return ambient + diffuse + specular;
}

vec3 CalcPointLight(PointLight light, vec3 normal, vec3 fragPos, vec3 viewPos)
{
    vec3 lightDir = normalize(light.position - fragPos);
    vec3 viewDir = normalize(viewPos - fragPos);
    vec3 halfwayDir = normalize(lightDir + viewDir);
    float diff = max(dot(normal, lightDir), 0.0);

    float spec = pow(max(dot(normal, halfwayDir), 0.0), material.shininess);

    float distance = length(light.position - fragPos);
    float attenuation = 1.0 /
        (light.constant + light.linear * distance +
         light.quadratic * distance * distance);

    vec3 texColor = texture(material.diffuse, fs_in.texCoord).rgb;

    vec3 ambient = light.ambient * texColor;
    vec3 diffuse = light.diffuse * diff * texColor;

    vec3 specular = vec3(0.0);
    if (material.useSpecular) {
        specular = light.specular * spec *
                   texture(material.specular, fs_in.texCoord).rgb;
    }

    ambient *= attenuation;
    diffuse *= attenuation;
    specular *= attenuation;

    return ambient + diffuse + specular;
}

vec3 CalcSpotLight(SpotLight light, vec3 normal, vec3 fragPos, vec3 viewDir)
{
    vec3 lightDir = normalize(light.position - fragPos);
    float diff = max(dot(normal, lightDir), 0.0);

    vec3 reflectDir = reflect(-lightDir, normal);
    float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);

    float distance = length(light.position - fragPos);
    float attenuation = 1.0 /
        (light.constant + light.linear * distance +
         light.quadratic * distance * distance);

    float theta = dot(lightDir, normalize(-light.direction));
    float epsilon = light.cutOff - light.outerCutOff;
    float intensity = clamp((theta - light.outerCutOff) / epsilon, 0.0, 1.0);

    vec3 texColor = texture(material.diffuse, fs_in.texCoord).rgb;

    vec3 ambient = light.ambient * texColor;
    vec3 diffuse = light.diffuse * diff * texColor;

    vec3 specular = vec3(0.0);
    if (material.useSpecular) {
        specular = light.specular * spec *
                   texture(material.specular, fs_in.texCoord).rgb;
    }

    ambient *= attenuation * intensity;
    diffuse *= attenuation * intensity;
    specular *= attenuation * intensity;

    return ambient + diffuse + specular;
}